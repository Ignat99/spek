apply from: "$rootDir/gradle/common/dependencies.gradle"
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'kotlin-platform-jvm'

def buildMatrix = [
    AS32: [prefix: 'Studio3.2', since: '181.5540', until: '181.5540.*', deps: ['org.jetbrains.kotlin:1.3.0-release-Studio3.2-1']],
    AS33: [prefix: 'Studio3.3', since: '182.5107', until: '182.5107.*', deps: ['org.jetbrains.kotlin:1.3.0-release-Studio3.3-1']],
]

String sdkVersion = "AS33"

if (hasProperty('as.version')) {
    sdkVersion = getProperty('as.version')
}

def settings = buildMatrix[sdkVersion]

String localIdePath = getProperty("local${sdkVersion}Path")

intellij {
    pluginName = "Spek Framework"
    localPath = "$localIdePath"
    plugins = ['gradle'] + settings['deps']
    alternativeIdePath = "$localIdePath"
}

dependencies {
    compile project(':spek-ide-plugin:intellij-base-jvm')
    compileOnly fileTree(dir: "$localIdePath/plugins/android/lib", include: ['*.jar'])
}

buildPlugin {
    baseName = ''
    version = "${project.version}-${settings['prefix']}"
}

patchPluginXml {
    version = "${project.version}-${settings['prefix']}"
    sinceBuild = settings['since']
    untilBuild = settings['until']
}
